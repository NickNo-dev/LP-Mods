WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    
    Player.dress()
    Player.show()
    
    MainActor = CurrentCompanion
    name = "companion"
    If MainActor.isValid()
        name = "<MainActor.name>"
        MainActor.dress()
        MainActor.show(1)
    EndIf
    
    Player::"What do I want to do?"
    0:: MainActor.isValid()::  "Hire <name> as new web cam model"
    1:: "Hire a new web cam model"
    2:: "Manage existing cam models"
    99::"Nothing"
    
    If 0 || 1
        followUp(nn_pe_scene_ask_to_become_web_cam_model)
    ElseIf 2
        "Please select the cam model that you want to manage."
        selectNPC(PEWebCam)
        MainActor = getSelectedNPC()
        
        If MainActor.isValid()
            
            MainActor.dress()
            MainActor.show(1)

            camState = MainActor.getActorVar(pe_DeclinedDoingWebCam)    // 99 = agreed to do camming
            askingPrice = MainActor.getActorVar(pe_EmployeeSalary)
            webcammingFrequency = MainActor.getActorVar(pe_WebCammingFrequency)
            fame = MainActor:pornfame
            
            traningPrice = 3000 * Random(0.8 , 1)   // Some discount?
            traningPriceLocal = traningPrice.convertToLocalCurrency(true)
            
            If camState == 99 
                
                // ** SUMMARY **
                cost = askingPrice * webcammingFrequency
                costLocal = cost.convertToLocalCurrency(true)
                Player(Serene)::"<MainActor.Name> does <webcammingFrequency> cam show per week. I pay <costLocal> per week for <MainActor.o> services. <MainActor.P> skill level is <fame>%." 

                stayInMenu = true
                While stayInMenu
                    Player::"What do I want to do?"
                    0:: !MainActor.isProstitute():: "Negotiate on the session price"
                    1:: "Change how often <MainActor.Name> does a cam show"
                    2:: "Book some ads to promote <MainActor.Name> for <traningPriceLocal>"
                    50:: "Watch <MainActor.Name>'s cam show"
                    51:: "Train <MainActor.Name> live on cam"
                    99:: "Nothing right now"
                    
                    If 0
                        daysSinceBirth = (Player:Age * 365) // get the current date 
                        last = MainActor.getActorVar(pe_LastSalaryNegotiation)
                        MainActor.setActorVar(pe_LastSalaryNegotiation, daysSinceBirth) // remember last negotiation
                        delta = daysSinceBirth - last
                        If delta < 30
                            timeToWait = 30 - delta
                            Player(Annoyed)::"I already negotiated on <MainActor.Name>'s salary this month. I will have to wait <timeToWait> days."
                        Else
                            chance = Random(1,5)
                            If chance >= 3 && askingPrice > 400
                                askingPrice -= (chance*10)
                                If askingPrice < 100
                                    askingPrice = 100
                                EndIf
                                askingPriceLocal = askingPrice.convertToLocalCurrency(true)
                                Player(Wink)::"I was able to talk <MainActor.Name> down to <askingPriceLocal> per session."
                                
                                MainActor.setActorVar(pe_EmployeeSalary, askingPrice)
                            Else
                                Random
                                    Player(Annoyed)::"<MainActor.Name> was not willing to negotiate."
                                    Player(Annoyed)::"I was out of luck trying to negotiate with <MainActor.Name>."
                                    Player(Annoyed)::"<MainActor.Name> did not accept my request to pay less for <MainActor.o> services."
                                EndRandom
                            EndIf
                        EndIf
                        
                        // ** SUMMARY **
                        cost = askingPrice * webcammingFrequency
                        costLocal = cost.convertToLocalCurrency(true)
                        Player(Serene)::"I will pay <costLocal> for <webcammingFrequency> sessions per week."
                        
                    ElseIf 1
                        
                        askingPriceLocal = askingPrice.convertToLocalCurrency(true)
                        Player(Serene)::"How often do I want <MainActor.Name> to entertain my visitors? (<askingPriceLocal>/session)"
                        
                        1:: "Once a week"
                        2:: "Twice a week"
                        3:: "Three times a week"
                        4:: "Four times a week"
                        5:: "Five times a week"
                        7:: "Each day of the week"
                        0:: "Suspend <MainActor.name>'s cam shows"
                        
                        webcammingFrequency = choice
                        
                        MainActor.setActorVar(pe_WebCammingFrequency, webcammingFrequency)
                        
                        // ** SUMMARY **
                        cost = askingPrice * webcammingFrequency
                        costLocal = cost.convertToLocalCurrency(true)
                        Player(Serene)::"I will pay <costLocal> for <webcammingFrequency> sessions per week."
                    ElseIf 2
                        last = MainActor.getActorVar(pe_LastJobTraining)
                        daysSinceBirth = (Player:Age * 365) // get the current date 
                        delta = daysSinceBirth - last
                        
                        If delta < 7
                            timeToWait = 7 - delta
                            Player(Annoyed)::"I already booked ads for <MainActor.Name> this week. I will have to wait <timeToWait> days."
                        Else
                            Player(Serene)::"<MainActor.Name> needs some more reputation to increase <MainActor.p> visitor numbers. Pay <traningPriceLocal> for some ads?"
                            0:: "Yes"
                            1:: "No"
                            
                            If 0
                                MainActor.setActorVar(pe_LastJobTraining, daysSinceBirth) // remember last training
                                
                                MainActor.dress()
                                MainActor.show(1)
                                
                                Random
                                    Player(Serene)::"Listen, <MainActor.Name> I booked some ads for your cam shows to get you more visitors."
                                    Player(Serene)::"<MainActor.Name>, I had a great discount on some on-line advertisements and booked one for you as well."
                                EndRandom
                                
                                money -= traningPrice
                                gain = Random(0.5,4)
                                MainActor:pornfame += gain
                                
                                If debugModeEnabled > 0 
                                    gain *= 1000
                                    "Debug TRANING: gain=<gain>/1000"
                                EndIf
                                
                                Random
                                    MainActor(Serene)::"Wow! Thanks for doing that for me!"
                                    MainActor(Serene)::"That's cool. I hope I get lots of new viewers!"
                                    MainActor(Flirty)::"Great! But if I get more viewers we will have to talk about my salary as well."
                                EndRandom
                            EndIf
                        EndIf
                    ElseIf 50   // Watch cam show
                        Random
                            Player()::"Let's see how <MainActor.Name> performs..."
                            Player()::"Let me see if <MainActor.Name> is worth the money I pay <MainActor.o>."
                            Player()::"<MainActor.Name> does not know that I watch her show. Let's see how <MainActor.s> does..."
                        EndRandom
                        
                        MainActor.strip()
                        Sex(MainActor)
                    ElseIf 51
                        Random
                            Player()::"<MainActor.Name>, I think you need some advanced training. Let me show you how to do this!"
                            Player()::"<MainActor.Name>, we need to bump your visitor numbers. Let's do a cam show together and improve your skills."
                            Player()::"<MainActor.Name>, honey. You don't mind your boss joining one of your live shows, do you? I'll show you how to make your audience horny!"
                        EndRandom
                        
                        Player.strip()
                        MainActor.strip()
                        SexNoAffair(Player, MainActor)
                        
                        change = Random(1,3)
                        MainActor:pornfame += change
                        Player:pornfame += change
                    Else
                        stayInMenu = false
                    EndIf
                EndWhile
            Else
                Player(Confused)::"<MainActor.Name> is not one of my cam models."
                Player(Confused)::"Maybe I should ask <MainActor.o> to be become one?"
            EndIf
        EndIf
        followUp( nn_pe_scene_manage_web_cam_models )
    ElseIf 99
        followUp( nn_pe_scene_manage_employees )
    EndIf
    
    passTime(1,2)
    
    SceneEnd()